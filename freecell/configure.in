dnl Process this file with autoconf to produce a configure script.

dnl ****************************************************************
dnl **************** configure.in of Freecell ****************
dnl ****************************************************************

AC_INIT(src/freecell.c)
AM_CONFIG_HEADER(config.h)
AC_PREREQ(2.12)
AM_INIT_AUTOMAKE([freecell], [0.7])

AM_MAINTAINER_MODE

dnl **************** Set available languages. ****************
dnl ALL_LINGUAS="ko"
ALL_LINGUAS="ko"

dnl **************** Checks for programs. ****************
AC_ISC_POSIX
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AM_PROG_LIBTOOL

dnl **************** I18N matters ****************
AM_GNU_GETTEXT
AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)
AC_CHECK_HEADER(libintl.h, [ AC_DEFINE(HAVE_LIBINTL_H) ])

dnl **************** Checks for header files. ****************
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h unistd.h)

dnl **************** Checks for typedefs, structures, ****************
dnl **************** and compiler characteristics. ****************
AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

dnl **************** Check GNOME. ****************

dnl It is now GNOME only.  Check whether it is in GNOME tree
dnl (in programs/freecell) or it is built out of the box.

dnl GNOME's configure export USE_GNOME when freecell is built in GNOME tree.

if test "$gnome_cv_use_gnome" = "yes"
then
  AC_MSG_RESULT([OK, built in GNOME tree.])
  in_gnome_tree="yes"
  GTK_LIBS="$GNOMEUI_LIBS $GNOMELIB_DIR $GNOMEINCLUDE_DIR"
else
  AC_MSG_RESULT([OK, built out of GNOME tree.])
  in_gnome_tree="no"

  dnl **************** Find the X11. ****************
  AC_PATH_X
  AC_PATH_XTRA
  X_LIBS=-L$x_libraries
  AC_SUBST(X_LIBS)

  dnl * GTK might be in X11 library directory?
  saved_LDFLAGS=$LDFLAGS
  LDFLAGS=$X_LIBS

  AC_CHECK_LIB(X11, XOpenDisplay,
               x_libs="-lX11 $X_EXTRA_LIBS"
	       ,
               [AC_MSG_ERROR(Can not find X11.)],
               $X_EXTRA_LIBS)
  AC_SUBST(x_libs)

  dnl **************** Find GTK. ****************
  AC_CHECK_LIB(gtk, gtk_init,
               GTK_LIBS="-lgtk -lgdk -lglib -lm"
	       ,
               [AC_MSG_ERROR(Can not find gtk/gdk/glib.)]
	       ,
               -lgdk -lglib $x_libs -lm)
  AC_SUBST(GTK_LIBS)

  dnl **************** Find GNOME. ****************
  AC_CHECK_LIB(gnomeui, gnome_init,
               GNOME_LIBS="-lgnomeui -lgnome -lSM -lICE"
               ,
               [AC_MSG_ERROR(Can not find GNOME.)]
	       ,
               -lgnome -ldl $GTK_LIBS $x_libs -lSM -lICE)
  AC_SUBST(GNOME_LIBS)

  dnl ********* Check for libSM, assume no libICE if no libSM *********
  AC_CHECK_LIB(SM, SmcSaveYourselfDone,
	[x_libs="$x_libs -lSM -lICE"], ,
	$x_libs -lICE)

  LDFLAGS=$saved_LDFLAGS
fi

AM_CONDITIONAL(IN_GNOME_TREE, test x$in_gnome_tree = xyes)

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_FUNC_MMAP
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(getcwd getwd putenv strerror)

AC_OUTPUT([Makefile src/Makefile lib/Makefile
	   image/Makefile intl/Makefile po/Makefile.in],
[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
